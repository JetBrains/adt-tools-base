dependencies {
    compile project(':common')
    testCompile 'junit:junit:3.8.1'
}

group = 'com.android.tools'
archivesBaseName = 'dvlib'

// configure the manifest of the buildDistributionJar task
buildDistributionJar.manifest.attributes("Main-Class": "com.android.validator.DeviceValidator")

jar {
    from 'NOTICE'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                if (!project.has("release")) {
                    throw new StopExecutionException("uploadArchives must be called with the release.gradle init script")
                }

                signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.ext.sonatypeUsername, password: project.ext.sonatypePassword)
            }

            pom.project {
                name 'Android Tools dvlib'
                description 'A Library to manage the Android device database XML files.'
                url 'http://tools.android.com'
                inceptionYear '2007'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                scm {
                    url "https://android.googlesource.com/platform/tools/base"
                    connection "git://android.googlesource.com/platform/tools/base.git"
                }
                developers {
                    developer {
                        name 'The Android Open Source Project'
                    }
                }
            }
        }
    }
}
