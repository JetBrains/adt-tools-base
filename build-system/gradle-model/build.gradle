apply plugin: 'java'
apply plugin: 'clone-artifacts'
apply plugin: 'idea'

def toolingApiVersion = gradle.gradleVersion

// Custom config that cloneArtifact will not look into, since this
// artifact is not in mavenCentral, but in the gradle repo instead.
configurations{
  gradleRepo
}

dependencies {
    compile project(':base:builder-model')

    testCompile 'junit:junit:3.8.1'
    testCompile project(':base:builder')
    testCompile project(':base:project-test-lib')

    // Need an SLF4J implementation at runtime
    testRuntime 'org.slf4j:slf4j-simple:1.7.2'
    testRuntime fileTree(dir: rootProject.file("base/build-system/tests"))

    // this is technically testCompile, but we don't want
    // it in there to avoid breaking cloneArtifact.
    // we'll add it to the test compile classpath manually below
    gradleRepo "org.gradle:gradle-tooling-api:${toolingApiVersion}"
}

//Include custom for compilation
sourceSets.test.compileClasspath += configurations.gradleRepo
sourceSets.test.runtimeClasspath += configurations.gradleRepo

test.dependsOn ':publishLocal'

idea {
    module {
        scopes.COMPILE.plus += [ configurations.gradleRepo ]
    }
}

test {
    environment("CUSTOM_REPO", rootProject.file("../out/repo"))
}
