ext {
    buildToolsVersion = System.env.CUSTOM_BUILDTOOLS != null ? System.env.CUSTOM_BUILDTOOLS : '20.0.0'
}

buildscript {
    def gradleVersion = System.env.CUSTOM_GRADLE != null ? System.env.CUSTOM_GRADLE : '0.14.4'

    repositories {
        if (System.env.CUSTOM_REPO != null) {
            maven { url System.env.CUSTOM_REPO }
        } else {
            mavenCentral()
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
    }
}


// Create subproject with the same source directories.  Each subproject will use a different stl
// library.
subprojects {
    apply plugin: 'com.android.model.application'

    model {
        android {
            compileSdkVersion 19
            buildToolsVersion = rootProject.ext.buildToolsVersion

            buildTypes.debug.jniDebugBuild true

            sourceSets {
                androidTest {
                    java {
                        srcDir '../src/androidTest/java'
                    }
                }
                main {
                    manifest.srcFile '../src/main/AndroidManifest.xml'
                }
            }
        }

        android.ndk {
            moduleName "hello-jni"
        }

        sources {
            main {
                res {
                    source {
                        srcDir '../src/main/res'
                    }
                }
                java {
                    source {
                        srcDir '../src/main/java'
                    }
                }
                cpp {
                    source {
                        srcDir '../src/main/jni'
                    }
                }
            }
        }
    }
}
