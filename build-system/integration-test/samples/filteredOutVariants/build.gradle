ext {
    buildToolsVersion = System.env.CUSTOM_BUILDTOOLS != null ? System.env.CUSTOM_BUILDTOOLS : '20.0.0'
}

buildscript {
    def gradleVersion = System.env.CUSTOM_GRADLE != null ? System.env.CUSTOM_GRADLE : '1.0.1'

    repositories {
        if (System.env.CUSTOM_REPO != null) {
            maven { url System.env.CUSTOM_REPO }
        } else {
            mavenCentral()
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion = rootProject.ext.buildToolsVersion

    variantFilter {
        String abi = it.flavors.get(0).name
        if ("cupcake".equals(it.flavors.get(1).name) && ("x86".equals(abi) || "mips".equals(abi))) {
            it.ignore = true
        }
    }

    flavorDimensions "abi", "api"

    productFlavors {
        x86 {
            flavorDimension "abi"
        }
        mips {
            flavorDimension "abi"
        }
        arm {
            flavorDimension "abi"
        }
        cupcake {
            flavorDimension "api"
        }
        gingerbread {
            flavorDimension "api"
        }
    }
}
