ext {
    buildToolsVersion = System.env.CUSTOM_BUILDTOOLS != null ? System.env.CUSTOM_BUILDTOOLS : (project.hasProperty("CUSTOM_BUILDTOOLS") ? project.CUSTOM_BUILDTOOLS : '20.0.0')
    useJack = System.env.CUSTOM_JACK != null ? true : (project.hasProperty("CUSTOM_JACK") ? true : false)
}

buildscript {
    def gradleVersion = System.env.CUSTOM_GRADLE != null ? System.env.CUSTOM_GRADLE : '1.2.0-alpha1'

    repositories {
        if (System.env.CUSTOM_REPO != null) {
            maven { url System.env.CUSTOM_REPO }
        } else {
            mavenCentral()
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 21
    buildToolsVersion = rootProject.ext.buildToolsVersion

    testBuildType rootProject.ext.useJack ? "debug" : "minified"

    defaultConfig {
        versionCode 12
        versionName "2.0"
        minSdkVersion 16
        targetSdkVersion 16
    }

    buildTypes {
        minified.initWith(buildTypes.debug)
        minified {
            minifyEnabled true
            useJack rootProject.ext.useJack
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "proguard-rules.pro"
            testCoverageEnabled true
        }
    }

    dexOptions {
        incremental false
    }

    lintOptions {
        abortOnError !rootProject.ext.useJack
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
}
