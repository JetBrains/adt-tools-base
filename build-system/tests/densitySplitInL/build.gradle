buildscript {
  repositories {
    maven { url '../../../../../out/repo' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.13.0'
  }
}

apply plugin: 'com.android.application'

android {
  compileSdkVersion 'android-L'
  buildToolsVersion '21.0.0'

  defaultConfig {
    versionCode 12
    minSdkVersion 'L'
    targetSdkVersion 'L'
  }

  splits {
    density {
      enable true
      exclude "ldpi", "tvdpi", "xxxhdpi"
    }
  }
}

// map for the version code
ext.versionCodes = [all:1, mdpi:2, hdpi:3, xhdpi:4, xxhdpi:5]

android.applicationVariants.all { variant ->
  // assign different version code for each output
  variant.outputs.each { output ->
    def key = output.densityFilter == null ? "all" : output.densityFilter
    output.versionCodeOverride = project.ext.versionCodes.get(key) * 100 + android.defaultConfig.versionCode
  }
}
