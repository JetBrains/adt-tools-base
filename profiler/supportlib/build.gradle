/*
 * Copyright (C) 2015 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven'

group = 'com.android.tools'
archivesBaseName = 'studio-profiler-lib'
version = '1.0'// TODO: Pass in as an argument

File androidJar = file('../../../../prebuilts/sdk/23/android.jar');
if (!androidJar.exists()) {
    throw new RuntimeException("android-23 android.jar not found at " + androidJar.absolutePath)
}

configurations {
    provided
    aar
    jni
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    provided files(androidJar)
//  TODO: Uncomment to include the jni libraries once they compile from source tree
//    jni project(path: ":native", configuration: "perfa")
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

jar {
    archiveName 'classes.jar'
}

// turn off strict javadoc
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}


task assembleAar(type:Zip, dependsOn: jar) {
    archiveName "profilers-support-lib"

    from "src/aar"
    from jar

//  TODO: Uncomment to include the jni libraries once they compile from source tree
//  from configurations.jni

    extension "aar"
}

artifacts {
    aar assembleAar
}

//// Publishing this library goes directly to a directory seen by studio at development time,
//// and forms part of the components bundled with studio at build time.
def repo = "$rootDir/../../../out/studio/repo/"

uploadAar {
    repositories {
        mavenDeployer {
            repository(url: "file://$repo")
        }
    }
    outputs.dir repo
}

build.dependsOn uploadAar
