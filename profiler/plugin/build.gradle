apply plugin: 'java'
apply plugin: 'maven'

group = 'com.android.tools'
archivesBaseName = 'studio-profiler-plugin'
version = '1.0'// TODO: Pass in as an argument

configurations {
    support
}

dependencies {
    compile gradleApi()
    compile 'org.ow2.asm:asm:5.0.3'
    compile 'com.google.guava:guava:17.0'
    compile('com.android.tools.build:gradle-api:2.0.0-beta2')

    support project(':supportlib')
}
jar {
    from configurations.support
}

//// Publishing this library goes directly to a directory seen by studio at development time,
//// and forms part of the components bundled with studio at build time.
def repo = "$rootDir/../../../out/studio/repo/"

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://$repo")
        }
    }
    outputs.dir repo
}

// We have a small set of dependencies, so copy them manually
def deps = [
        "org/ow2/asm/asm/5.0.3",
        "com/google/guava/guava/17.0",
        "com/android/tools/build/gradle-api/2.0.0-beta2",
        "com/android/tools/annotations/25.0.0-beta2",
]

deps.each { name ->
    // create a task that copies some additional data in the library bundle
    def copyTask = tasks.create(name: "copy$name", type: Copy) {
        from           file("../../../../prebuilts/tools/common/m2/repository/" + name)
        destinationDir file("$repo/$name")
    }
    uploadArchives.dependsOn copyTask
}
build.dependsOn uploadArchives
